
plugins {
    id("nu.studer.jooq")
}

val jooqVersion = "3.19.15"

// Extract tables from migrations
val migrationsDir = file("src/main/resources/${project.name}-migrations/postgres")
val extractedTables = SqlMigrationParser.extractTableNames(migrationsDir)

// Exit early if no tables found
if (extractedTables.isEmpty()) {
    println("jOOQ: No tables found for ${project.name}, skipping jOOQ configuration")
} else {
    val dynamicIncludes = SqlMigrationParser.generateIncludes(extractedTables)

    println("jOOQ: Extracted tables for ${project.name}: $extractedTables")

    dependencies {
        // Get the version catalog
        val libs = project.extensions.getByType<VersionCatalogsExtension>().named("libs")

        // Only core should bundle jOOQ, other modules use it from core at runtime
        val isCoreProject = project.name == "core"

        if (isCoreProject) {
            // Core bundles jOOQ runtime
            add("api", libs.findLibrary("jooq").get())
            add("api", libs.findLibrary("jooq.meta").get())
            add("implementation", libs.findLibrary("postgres").get())
        } else {
            // Non-core projects only need jOOQ at compile time
            add("compileOnly", libs.findLibrary("jooq").get())
        }

        // Generator dependencies are always needed for code generation
        add("jooqGenerator", libs.findLibrary("jooq.meta").get())
        add("jooqGenerator", libs.findLibrary("jooq.meta.extensions").get())
        add("jooqGenerator", "org.postgresql:postgresql:42.7.4")
    }

    jooq {
        version.set(jooqVersion)

        configurations {
            create("main") {
                generateSchemaSourceOnCompilation.set(false)
                jooqConfiguration.apply {
                    jdbc.apply {
                        driver = "org.postgresql.Driver"
                        url = "jdbc:postgresql://localhost:5002/betterpvp"
                        user = "user"
                        password = "BetterPvP123!"
                    }
                    generator.apply {
                        name = "org.jooq.codegen.DefaultGenerator"
                        database.apply {
                            name = "org.jooq.meta.postgres.PostgresDatabase"
                            inputSchema = "public"
                            includes = dynamicIncludes
                            excludes = ".*_\\d+|.*_\\d+_\\d+"
                            withForcedTypes(
                                org.jooq.meta.jaxb.ForcedType().apply {
                                    setName("INTEGER")
                                    setIncludeTypes("SMALLINT")
                                }
                            )
                        }
                        generate.apply {
                            isDeprecated = false
                            isRecords = true
                            isImmutablePojos = false
                            isFluentSetters = true
                        }
                        target.apply {
                            packageName = "me.mykindos.betterpvp.${project.name}.database.jooq"
                            directory = "build/generated-jooq/"
                        }
                    }
                }
            }
        }
    }

    // Task to move generated jOOQ files to src/main/java
    val moveJooqGenerated by tasks.registering {
        group = "jooq"
        description = "Moves jOOQ generated files to src/main/java for ${project.name}"

        doLast {
            val sourceDir = file("build/generated-jooq/me/mykindos/betterpvp/${project.name}/database/jooq")
            val targetDir = file("src/main/java/me/mykindos/betterpvp/${project.name}/database/jooq")

            // Clean the target directory before moving
            if (targetDir.exists()) {
                println("Cleaning jOOQ directory: ${targetDir.absolutePath}")
                targetDir.deleteRecursively()
            }

            // Move files from build to src
            if (sourceDir.exists()) {
                println("Moving jOOQ files from ${sourceDir.absolutePath} to ${targetDir.absolutePath}")
                sourceDir.copyRecursively(targetDir, overwrite = true)
                sourceDir.deleteRecursively()
                println("jOOQ files moved successfully")
            } else {
                println("Source directory does not exist: ${sourceDir.absolutePath}")
            }
        }
    }

    // Make the move task run after jOOQ generation
    afterEvaluate {
        tasks.named("generateJooq") {
            finalizedBy(moveJooqGenerated)
        }

        // For non-core projects, exclude jOOQ from runtime configurations
        if (project.name != "core") {
            configurations.named("runtimeClasspath") {
                exclude(group = "org.jooq")
            }
            configurations.named("runtimeOnly") {
                exclude(group = "org.jooq")
            }
            // Also exclude from the jar task
            tasks.named("jar", Jar::class) {
                exclude("org/jooq/**")
            }
        }
    }
}